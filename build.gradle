plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
    id 'idea'
}

group = 'org.monospark'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    isCiServer = System.getenv().containsKey('JENKINS_HOME')
    isSnapshot = version.endsWith('SNAPSHOT')
}

repositories {
    mavenCentral()
	maven {
		url "http://repo.spongepowered.org/maven/"
	}
    maven {
        url "https://repo.monospark.org/content/groups/public/"
    }
}

configurations {
    customCheckstyle
}

dependencies {
	compile 'org.spongepowered:spongeapi:3.0.0'
    runtime 'org.monospark:spongematchers:1.0'
    testCompile 'junit:junit:4.11'
    customCheckstyle 'com.puppycrawl.tools:checkstyle:6.13'
}

checkstyle {
    //Only run checkstyle tests for main
    sourceSets = Collections.singleton(sourceSets.main)
    //Checkstyle config is not located at the default location, so it has to be changed
    configFile = file('checkstyle/checkstyle.xml')
    configProperties = [
        'basedir': project.projectDir,
    ]
}

tasks.checkstyleMain {
    //Use a custom version of checkstyle
    checkstyleClasspath = files(configurations.customCheckstyle.files)
}

//Include the license file in the jar
jar {
    from 'LICENSE.txt'
    rename 'LICENSE.txt', 'objectdiff-license.txt'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

if(isCiServer) {
    apply from: 'deploy.gradle'
}
